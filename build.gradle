apply plugin: 'java'

group = 'cd.go'
version = '0.0.1'

project.ext.pluginDesc = [
        id         : 'cd.go.authorization.skeleton',
        version    : project.version,
        goCdVersion: '19.2.0',
        name       : 'Authorization Skeleton Plugin for GoCD',
        description: 'Authorization Skeleton Plugin for GoCD',
        vendorName : 'GoCD Contributors',
        vendorUrl  : 'https://github.com/gocd/authorization-sekeleton-plugin'
]

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:23.4.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    testImplementation project.deps.gocdPluginApi
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.7.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.16.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}